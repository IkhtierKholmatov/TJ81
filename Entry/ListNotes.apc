{Application 'ListNotes' logic file generated by CSPro }
PROC GLOBAL

{-------------------------------------------------------------------------}
{          This application generates an HTML report file                 }
{ The application uses functions declared in \Library\HTMLFunctions.apc.  }
{ All functions declared in that file start with the letters HTML_        }
{ It also uses the HTMLFile file declaration and a two arrays of strings: }
{    -CellValues(). Contains the values of an entire row for a report     }
{    -CellAlign().  Specifies the n cell that is left aligned             }
{-------------------------------------------------------------------------}

  string  line;
  numeric firsthh, firstin, xfield, xdate, xtime, xnote, firstnote = 1, xcluster, i, requestby;
  FILE    FILENote;

PROC FL_CNULL
preproc

  { converts parameter to numbers  }
  xcluster  = tonumber( sysparm()[1:4] );   { cluster number }
  requestby = tonumber( sysparm()[5:1] );   { if report is for: interviewer(0), supervisor(1), central office(2)  }
  { get language from calling menu }
  SetLanguage( GetLanguage() );

  { set the name for the HTML file }
  HTML_InitAlign();
  HTMLname = "ListNotes.html";
  { following instructions not necessary because HTML will be output always to \Entry 
  if requestby = 1 then 
    HTMLname = "..\Superv\ListNotes.html";
  elseif requestby = 2 then 
    HTMLname = "..\Central\ListNotes.html"; 
  endif;
  }
  if FileExist( HTMLname ) then
    FileDelete( HTMLname )
  endif;
  setfile( HTMLFile, HTMLname, create );

  { specify columns that are left aligned, strings are left aligned }
  zstring1 = MakeText( tr("Listing Notes for Cluster: %04d"), xcluster );
  CellAlign(1) = 3;  // column 3 is filed name 
  CellAlign(2) = 6;  // column 5 is the actual note
  HTML_Header( CellAlign, zstring1 );  // generates a general HTML header
  { list of individuals and children eligible for individual and height & weight }
  firsthh   = 1;   // to identify the first time a household was found
  firstin   = 1;   // to identify the first time an individual was found
  xfield    = 0;   // position of name of the filed in the notes record
  xdate     = 0;   // position of the date in the notes record
  xnote     = 0;   // position of the actual note in the notes record
  firstnote = 1;   // to identify the first time a note was found
  while FileRead(FILENote, line) do
    if !pos( "~Case ID", line ) & length(line) then
	  if firstnote then
        CellValues(1) = tr("Household");
	    CellValues(2) = tr("Line");
    	CellValues(3) = tr("Field");
    	CellValues(4) = tr("Date (dd/mm/yyyy)");
    	CellValues(5) = tr("Time (hh:mm)");
    	CellValues(6) = tr("Note/Observation");
        HTML_BeginTable( CellValues, "", 6 );
		firstnote = 0;
      endif;
      CellValues(1) = line[5:4];    // Household is in position 5 in the record notes
      CellValues(2) = line[9:2];    // no line number for a hosuehold
      if xfield = 9 then CellValues(2) = "BLANK" endif;    // no line number for a hosuehold
      do i = xfield while line[i:1] <> " " enddo;
      CellValues(3) = line[xfield:i-xfield];
      CellValues(4) = line[xdate+6:2] + "/" + line[xdate+4:2] + "/" + line[xdate:4];
      CellValues(5) = line[xtime:2] + ":" + line[xtime+2:2];
      CellValues(6) = line[xnote:length(line)-xnote+1];
      if xfield = 9 & firsthh then          // it is the first note for households
        HTML_Subtitle( tr("Households"), 6 );
        firsthh = 0;
      elseif xfield = 11 & firstin then     // it is the first note for individuals
        HTML_Subtitle( tr("Individuals"), 6 );
        firstin = 0;
      endif;
      HTML_OneRow( CellValues, 6 );   // generates the HTML script for a row with relevant household data
    elseif pos( "~Case ID", line ) then
	  xfield = pos( "Field Name", line );
	  xdate  = pos( "Date", line );
	  xtime  = pos( "Time", line );
	  xnote  = pos( "Note", line );
	endif;
  enddo;
  
  { close table and HTML }
  if !firstnote then
    HTML_EndTable();
  else
    HTML_Paragraph( tr("No notes found in this cluster") );
  endif;
  HTML_End();
  view( "file://" + pathname(application) +  HTMLName );
  stop(1);

