PROC GLOBAL
{ CKID program master version - 1.4.0 of 12/19/03 }
  
  numeric heightdif;
  string  textdif, xcluster;

PROC FL_CNULL
preproc

  xcluster = sysparm()[1:4]; 
  SetLanguage( getlanguage() );

  { set the name for the HTML file }
  HTML_InitAlign();
  HTMLname = "RemeasLst.html";
  if FileExist( HTMLname ) then
    FileDelete( HTMLname )
  endif;
  setfile( HTMLFile, HTMLname, create );

  { initialize the report headings }
  zstring1 = MakeText( tr("CHILDREN SELECTED FOR REMEASUREMENT IN CLUSTER: %s"), xcluster );
  zstring2 = tr("Pay special attention to 'height difference'");
  CellValues(1)  = tr("HH");
  CellValues(2)  = tr("Line");
  CellValues(3)  = tr("Child Name");
  CellValues(4)  = tr("Household Address");
  CellValues(5)  = tr("Date Remeasure");
  CellValues(6)  = tr("Date of Birth");
  CellValues(7)  = tr("Weight");
  CellValues(8)  = tr("Height");
  CellValues(9)  = tr("Lying/Stand");
  CellValues(10) = tr("Diff-Height");
  { specify columns that are left aligned, strings are left aligned }
  CellAlign(1) = 3;   // column 2 child'sname 
  CellAlign(2) = 4;   // column 4 household address
  CellAlign(3) = 10;  // column 10 height-difference
  HTML_Header( CellAlign, zstring1 );  // generates a general HTML header

  HTML_BeginTable( CellValues, zstring2, 10 );  // generates the HTML table script with the column headers
  
  while loadcase( RMEASURE ) do  
	{ print information of the selected child }
	heightdif = RFHEIGHT - R108;
	if RTYPE = 2 | R108 = notappl then   { difference calculated only for random selection }
      textdif   = " "
	elseif heightdif in (-1):1 then 
      textdif   = tr("Acceptable");
	else
      textdif   = tr("Unacceptable");
	endif;
	CellValues(1)  = edit("9999",RNUMBER);
	CellValues(2)  = edit("99",R102L);
	CellValues(3)  = strip(R102N);
	CellValues(4)  = strip(RADDRESS);
	CellValues(5)  = edit("99",R115D) + "/" + edit("99",R115M) + "/" + edit("9999",R115Y);
	CellValues(6)  = edit("99",R103D) + "/" + edit("99",R103M) + "/" + edit("9999",R103Y);
	CellValues(7)  = edit("99.99",R106);
	CellValues(8)  = edit("999.9",R108);
	CellValues(9)  = edit("9",R109);
	CellValues(10) = textdif;
    HTML_OneRow( CellValues, 10 );   // generates the HTML script for a row with relevant household data
  enddo;
  HTML_EndTable();
  HTML_End();
  view( "file://" + pathname(application) + HTMLName );

  stop(1);
