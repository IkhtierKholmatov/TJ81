PROC GLOBAL
{ CKID program master version - 1.4.0 of 12/19/03 }

  numeric l, i, k, xdups;

  FILE        filedups;                     { file with duplicates listing }

  alpha(4) xcluster;                      { !!!! Cluster number, adjust according to survey cluster number of digits }
  alpha(4) xnumber;                       { !!!! household number, adjust according to household number of digits }
  alpha(2) xline;                         { Individual line number, it is normally 2 digits }
  string   recordstr;                     { to read string from listing file }
  string   fname;                         { file name }
  string   xtype;                         { type of questionnaire W-Woman, M-Man and line number }
  string   reslabel;                      { to display result of interview }

PROC FL_CNULL
preproc

  xcluster = sysparm()[1:4];                { cluster coming from SupMenu }
  SetLanguage( getlanguage() );

  { set the name for the HTML file }
  HTML_InitAlign();
  HTMLname = "ChkINDup.html";
  if FileExist( HTMLname ) then
    FileDelete( HTMLname )
  endif;
  setfile( HTMLFile, HTMLname, create );

  { initialize the report headings }
  zstring1 = MakeText( tr("DUPLICATED INDIVIDUALS FOUND IN CLUSTER: %s"), xcluster );
  zstring2 = tr("Problem(s) needs to be resolved before proceeding");
  CellValues(1) = tr("HH");
  CellValues(2) = tr("Sex-Line");
  CellValues(3) = tr("Individual's Name");
  CellValues(4) = tr("Interviewer");
  CellValues(5) = tr("Result");
  { specify columns that are left aligned, strings are left aligned }
  CellAlign(1) = 3;  // column 2 is individual name 
  CellAlign(2) = 4;  // column 3 is interviewer name
  CellAlign(3) = 5;  // column 4 is result of the interview
  HTML_Header( CellAlign, zstring1 );  // generates a general HTML header

  HTML_BeginTable( CellValues, zstring2, 5 );  // generates the HTML table script with the column headers

  while FileRead( filedups, recordstr ) do
    if pos( "*** Case [", recordstr ) = 1 then
    { gets cluster, household and individual line number to access them in the individual data file }
      xcluster = recordstr[11:4];    { !!!! adjust according to survey cluster number of digits }
      xnumber  = recordstr[15:4];    { !!!! adjust according to survey household number of digits }
      xline    = recordstr[19:2];    { !!!! adjust according to survey household number of digits }
      { read all records until going through all duplicates for the same households }
	  l = pos("has", recordstr);
	  l = l + 4;
	  do k = l while recordstr[k:1] <> " "
	  enddo;
	  xdups = tonumber(recordstr[l:k-l]);
      { read records going through all files with duplicates for the same individuals }
	  do i = 1 while i <= xdups 
        FileRead( filedups, recordstr );
        do k = 1 while( recordstr[k:1] = " " ) enddo;
        do l = k+1 while( recordstr[l:1] <> " " ) enddo;
        fname = recordstr[k:l-k];
        { sets the appropriate file to the individual dictionary }
        setfile( TJIN80, strip(fname) );
        if loadcase( TJIN80, xcluster, xnumber, xline ) then
          ICODE = QINTNUM;
          loadcase( INTERV, ICODE );
          xtype = tr("W-");
          if QQTYPE = 1 then xtype = tr("M-") endif;
          xtype = xtype + edit("99",QLINE);
          if partialcs( 2, QCLUSTER, QNUMBER, QLINE, fname ) then
            reslabel = tr("Partial");
          else
            reslabel = GetValueLabel(QRESULT);
          endif;
          { populates the elements of a row based on household information }
          CellValues(1) = edit("9999", QNUMBER);
          CellValues(2) = xtype;
          CellValues(3) = strip(QNAME);
          CellValues(4) = edit("9999",QINTNUM) + "-" + strip(INAME);
          CellValues(5) = edit("9", QRESULT) + "-" + reslabel;
          HTML_OneRow( CellValues, 5 );   // generates the HTML script for a row with relevant household data
        else
          zstring2 = MakeText( Tr("Case=%s%s%s not found in file: %s, check IDs in dict and logic"), xcluster, xnumber, xline, fname );
          HTML_Subtitle( zstring2, 5 );          
        endif;
      enddo;
    endif;
  enddo;
  HTML_EndTable();
  HTML_End();
  view( "file://" + pathname(application) + HTMLName );

  stop(1);
