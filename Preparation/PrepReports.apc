{Application 'PREPREPORTS' logic file generated by CSPro }
PROC GLOBAL

  numeric reptype, lines = 0, i;
  numeric linespage = 60;             { !!! change based on the type of paper to be used }

  string line1, line2, line3, line4;

  { arrays to check consistency between CAPI personnel and fieldwork questionnaires }
  array CAPIWorker( 1000 );
  array CAPIFound( 1000 );
  array QSTFound( 1000 );

  { keeps track of fieldworkers defined for CAPI and in fieldworker questionnaire }
  function CAPIFieldWorker( xcode )
    { first check if code from fieldworker questionnaire in CAPI }
    do i = 1 while CAPIWorker(i)
      if CAPIWorker(i) = xcode then
        CAPIFound(i) = 1;
        break;
      endif;
    enddo;
    { keep track of fieldworkers in questionnaire not in CAPI }
    if !CAPIWorker(i) then
      do i = 1 while QSTFound(i) enddo;
      QSTFound(i) = xcode;
    endif;
  end;

PROC CNULL_FF
preproc

  reptype = tonumber(sysparm()[1:1]);
  SetLanguage( GetLanguage() );

  if reptype = 1 then
    if GetLanguage() = "EN" then
      line1 = "                              LIST OF CLUSTERS INCLUDED IN THE SURVEY SAMPLE";
      line2 = "        Urban/                                                                                               Total";
      line3 = "Cluster Rural  Region                  State                   District                City                    HH";
	elseif GetLanguage() = "ES" then
      line1 = "                              LISTADO DE SEGMENTOS INCLUIDOS EN LA MUESTRA";
      line2 = "Segmen- Urban/                                                                                               Total";
      line3 = "  to    Rural  Region                  Estado                  Distrito                Ciudad                 Hog.";
    endif;
    line4 = "__________________________________________________________________________________________________________________";
    write( "%s", strip(line1) );
    write( "%s", strip(line2) );
    write( "%s", strip(line3) );
    write( "%s", strip(line4) );
    while loadcase( CLUSTERS ) do
      write(  " %04d   %-6s %02d-%-20s %02d-%-20s %02d-%-20s %02d-%-20s %d",
             YCLUSTER, GetLabel(YURBRUR,YURBRUR), YREGION, strip(YREGIONN), YSTATE, strip(YSTATEN), YDISTRICT, strip(YDISTRICTN), YCITY, strip(YCITYN), YTOTHH );
      lines = lines + 1;
      if lines > linespage then
        write( "" );             { page break }
        write( "%s", strip(line1) );
        write( "%s", strip(line2) );
        write( "%s", strip(line3) );
        write( "%s", strip(line4) );
        lines = 0;
      endif;
    enddo;
  elseif reptype = 2 then
    if GetLanguage() = "EN" then
      line1 = "     LIST OF PERSONNEL PARTICIPATING IN CAPI FIELDWORK OPERATION";
      line2 = "Code   Name                                  Team  Role            Sex";
	elseif GetLanguage() = "ES" then
      line1 = "    LISTADO DE PERSONAL QUE PARTICIPA EN EL TRABAJO DE CAMPO CAPI";
      line2 = "Codigo Nombre                               Equipo Role            Sexo";
    endif;
    line3 = "__________________________________________________________________________";
    while loadcase( INTERV ) do
      if !lines | lines > linespage then
        write( "" );             { page break }
        write( "%s", strip(line1) );
        write( "%s", strip(line2) );
        write( "%s", strip(line3) );
        lines = 4;
      endif;
      write(  "%04d   %s         %02d   %-15s %s", ICODE, INAME, ITEAM, GetLabel(IROLE,IROLE), GetLabel(ISEX,ISEX) );
      lines = lines + 1;
    enddo;
  elseif reptype = 3 then
    while loadcase( SAMPSEL ) do
      if GetLanguage() = "EN" then
        write( "                  LIST OF HOUSEHOLDS SELECTED FOR CLUSTER %04d", XCLUSTER );
        write( "  HH                                               Name of                    Selected for" );
        write( "Number Address                                  Household head                Male Anemia" );
      elseif GetLanguage() = "ES" then
        write( "                LISTADO DE HOGARES SELECCIONADOS PARA SEGMENTO %04d", XCLUSTER );
        write( "Numero                                            Nombre del                  Selecc. para" );
        write( "Hogar  Direccion                                Jefe del hogar                Homb Anemia" );
      endif;
      write( "____________________________________________________________________________________________" );
      do i = 1 while i <= XTOTAL
        write(  " %04d  %s %s %-3s  %-3s", XNUMBER(i), XADDRESS(i), XNAME(i), GetLabel(XMALE,XMALE(i)), GetLabel(XANEMIA,XANEMIA(i)) );
      enddo;
    enddo;
  elseif reptype = 4 then
    do i = 1 while loadcase( INTERV )
      CAPIWorker(i) = ICODE;
      CAPIFound(i)  = 0;
    enddo;
    if GetLanguage() = "EN" then
      line1 = "             LIST OF SELECTED VARIABLES FOR FIELD WORK PERSONNEL QUESTIONNAIRE";
      line2 = "                                            Education  Marital                              ";
      line3 = "Code Province                  Age  Sex    Level Years Status  Ethnicity           Language ";
	elseif GetLanguage() = "ES" then
      line1 = "    LISTADO DE VARIABLES SELECCIONADAS PARA CUESTIONARIO DE PERSONAL DE TRABAJO DE CAMPO";
      line2 = "Co-                                         Educacion  Estado                               ";
      line3 = "digo Provincia                 Edad Sexo   Nivel Años  Marital Etnicidad           Idioma   ";
    endif;
    line4 = "____________________________________________________________________________________________";
    while loadcase( FIELDWKQ ) do
      if !lines | lines > linespage then
        write( "%s", strip(line1) );
        write( "%s", strip(line2) );
        write( "%s", strip(line3) );
        write( "%s", strip(line4) );
        lines = 4;
      endif;
      CAPIFieldWorker( FW101 );      // !!! required only for CAPI surveys
      lines = lines + 1;
      write( "%04d %-24s  %02d   %-6s   %d    %02d     %d     %-20s%s",
             FW101, GetLabel(FW102,FW102), FW104, GetLabel(FW105,FW105),  FW109, FW110, FW106, GetLabel(FW112,FW112), GetLabel(FW113,FW113) );
    enddo;
    { report discrepancies identified between CAPI and fieldworker questionnaire }
    write( "" );
    write( "" );
    if GetLanguage() = "EN" then
      write( "Discrepancies identified between CAPI and fieldworker questionnaires" );
	elseif GetLanguage() = "ES" then
      write( "Discrepancias identificadas entre CAPI y cuestionario de trabajador de campo" );
	endif;
    write( "____________________________________________________________________________" );
    do i = 1 while CAPIWorker(i)
      if !CAPIFound(i) then
        if GetLanguage() = "EN" then
          write( "Fieldworker %d in CAPI not found in fieldworker questionnaire", CAPIWorker(i) );
        elseif GetLanguage() = "ES" then
          write( "Trabajador de campo %d en CAPI no se encontro en cuestionario de trabajador de campo", CAPIWorker(i) );
		endif;
      endif;
    enddo;
    write( "" );
    write( "________________________________________________________________________________________" );
    do i = 1 while QSTFound(i)
      if GetLanguage() = "EN" then
        write( "Fieldworker %d in questionnaire but doesn't exist for CAPI ", QSTFound(i) );
      elseif GetLanguage() = "ES" then
        write( "Trabajador de campo %d en cuestionario pero no existe para CAPI ", QSTFound(i) );
	  endif;
    enddo;
 
  endif;
