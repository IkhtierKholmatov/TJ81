{Application 'SELHHENT' logic file generated by CSPro }
PROC GLOBAL

  numeric i, maxsel;

  string spechar = "`1234567890=~!@#$%^&*()_+[]{};\:|,./<>?øóðõñ×öòú'''";
  string dquote  = '"';

  { set value sets based on language }
  function OnChangeLanguage()
    SetLanguage( getlanguage() );
  end;

  { setup basic user bar }
  function userbase();
    userbar( clear );
    userbar( add button, "<",    do("PreviousField") );
    userbar( add button, ">",    do("NextField") );
    userbar( add button, ">>|",  do("AdvanceToEnd") );
    userbar( add button, "Lang", do("ChangeLanguage") );
  end;

PROC SAMPSEL_FF
preproc
  maxsel = maxocc( SAMREC2 );      { maximum number of occurrences in household selected records }

  set attributes( SAMPSEL ) assisted off ( variable(title) );

  spechar = spechar + dquote;

  { set font for value sets }
  setfont( All, "Arial", 18, bold );

  { set up minimal user bar }
  userbase();
  userbar( show );
  { set the language at the start of the program, defaulting to English }
  SetLanguage( getlanguage() );

PROC SAMPSEL_QUES
  if demode() = add & accept(tr("Do you want to enter households for another cluster?"), 
                             tr("1-Yes"), tr("2-No") ) <> 1 then
    stop(1);
  endif;

PROC XCLUSTER

  if !loadcase( CLUSTERS, $ ) then
    errmsg( 150 );
    reenter;
  endif;

PROC XTOTAL
preproc
{ ***
  { !! if number of households per cluster is constant, consider setting this variable as protected }
  if VisualValue( $ ) = notappl then
    $ = 25;
    noinput;
  endif;
 *** }


postproc
  if $ <> YTOTHH then
    errmsg( 151, YTOTHH );
    if accept(tr("Do you want to change the total number of households?"), 
	          tr("1-Yes"), tr("2-No") ) = 1 then
      reenter;
    endif;
  endif;

PROC SAMREC2000
preproc
  { initialize date cluster selection was entered and interviewer code }
  XCLDATE = sysdate( "MMDDYYYY" );
  do i = 1 while i <= maxsel by 1
    if i <= XTOTAL then
      XINTCODE(i) = 0;
      XINTDATE(i) = 0;
      XRESULT(i) = 0;
    else
      XINTCODE(i) = notappl;
      XINTDATE(i) = notappl;
      XRESULT(i) = notappl;
    endif;
  enddo;

PROC XNUMBER
preproc
  if curocc() > XTOTAL then
    endsect;
  endif;

postproc
  { check that households aren't duplicated }
  do i = 1 while i < curocc() by 1
    if $(i) = $ then
      errmsg( 152, $, i );
      reenter;
    endif;
  enddo;


PROC XNAME
  if !length( strip($) ) | pos( $[1:1], strip(spechar) ) then
    errmsg( 153 );
  endif;

PROC XMALE
preproc
  { !! remove this variable if no male survey }
  { !! assign 1 and make the field protected if all households are eligible for the male questionnaire }

PROC XANEMIA
preproc
  { !! remove this variable if no anemia in the survey }
  { !! assign 1 and make the field protected if all households are eligible for anemia }
