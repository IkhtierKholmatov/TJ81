{Application 'VACENTRY' logic file generated by CSPro}
PROC GLOBAL
  string refDir, imageDir, photoConfig;
  numeric xcluster, xhhnum, xinterv, xsuperv, xmline, xcline, xccol, x, j, i, k, n, vcheckx;
  string vaccname;
  string textstr1;        { Alpha variable to get customize text for questions }
  string textstr2;        { Alpha variable to get customize text for questions }
  string id;              { questionnaire full ID }
  numeric dd, mm, yy;

  { setup basic user bar }
  function userbase();
    userbar( clear );
    userbar( add button, "<",    do("PreviousField") );
    userbar( add button, ">",    do("NextField") );
    userbar( add button, ">>|",  do("AdvanceToEnd") );
    userbar( add button, tr("Note"), do("EditNote") );
    userbar( add button, tr("Lang"), do("ChangeLanguage") );
	userbar( add button, tr("Ver"),  GetPubDate(2) );
  end;
  
  //checks if day is within bounds based on month/year
  function validDay(vday, vmonth, vyear)
    numeric res;
    
    numeric leap =  (vyear % 4 = 0);
    
    box vday : vmonth          : leap => res;
        1-31 : 1,3,5,7,8,10,12 :       => 1;
        1-30 : 4,6,9,11        :       => 1;
        1-28 : 2               :       => 1;
        29   : 2               : 1     => 1;
             :                 :       => 0; 
    endbox;
    
    validDay = res;
  end;
  
  { Function to check all possible combinations of day, month and year of vaccination in section 5 }
  function vcheck( vcheckd, vcheckm, vchecky )
    recode  vchecky         :: vcheckm               :: vcheckd    -> x;       { !!! }
                       9998 :: 98                    :: 98         -> 1; { can't all be 98's - would use 44 in that case }
        2015:2025,9997,9998 :: 1,3,5,7,8,10,12,97,98 :: 1:31,97,98 -> 0;
        2015:2025,9997,9998 :: 4,6,9,11              :: 1:30,97,98 -> 0;
        2015:2025,9997,9998 :: 2                     :: 1:28,97,98 -> 0;
        2016,2020,2024      :: 2                     :: 29         -> 0;       { Leap year }
                 6666       :: 66                    :: 66         -> 0;
                 4444       :: 44                    :: 44         -> 0;
                    0       :: 0                     :: 0          -> 0;
                            ::                       ::            -> 1;
    endrecode;
    if vchecky < 2050 & vchecky > QINTY then
	  x = 2
    elseif vchecky = QINTY & vcheckm <= 12 & vcheckm > QINTM then
      x = 2
    elseif vchecky = QINTY & vcheckm = QINTM &
           vcheckd <= 31 & vcheckd > QINTD then
      x = 2
    endif;
    vcheck = x;
	if x then
	  errmsg( 4470, vcheckd, vcheckm, vchecky );
      reenter;
    endif;
  end;

  { check that a given date is greater or equal to child's date of birth }
  function vckbirth( vcheckd, vcheckm, vchecky, chidx )
    vcheckx = 0;
    if vchecky in 2015:2025 & Q220Y(chidx) in 2015:2025 & vchecky < Q220Y(chidx) then
      vcheckx = 1
    elseif vchecky in 2015:2025 & vchecky = Q220Y(chidx) &
           vcheckm in 1:12 & Q220M(chidx) in 1:12 & vcheckm < Q220M(chidx) then
      vcheckx = 1
    elseif vchecky in 2015:2025 & vchecky = Q220Y(chidx) &
           vcheckm in 1:12 & Q220M(chidx) = vcheckm &
           vcheckd in 1:31 & Q220D(chidx) in 1:31 & vcheckd < Q220D(chidx) then
      vcheckx = 1
    endif;
    vckbirth = vcheckx;
	if vcheckx then
      errmsg( 4471, vcheckd, vcheckm, vchecky, Q503, Q220D(Q503), Q220M(Q503), Q220Y(Q503) );
      reenter;
    endif;
  end;

  { list all pictures and use the last last one taken }
  function string GetPicture()
    string picfile;
    list string PictureList;
    picfile = "C:/Users/" + GetUserName() + "/OneDrive/Pictures/Camera Roll";
    dirlist( PictureList, picfile, "WIN*.JPG" );
    do j = 1 while j <= PictureList.length()
      Textstr2 = PictureList(j);
    enddo;
    GetPicture = Textstr2;
  end;

  { function to take picture with a windows tablet }
  function TakePictureW( child )
    image VCardPic;
    { take as many pictures as necessary }
    do i = 1 while i
      execsystem( "cmd /c start microsoft.windows.camera:", wait ); 	
      display( tr("Click OK to continue") );	  
      textstr1 = GetPicture();
      VCardPic.load(textstr1);
      VCardPic.resample(width := 800, height := 600);
	  FileDelete( textstr1 );
      { find if there are pictures previusly taken for that child  and generate different versions }
      textstr2 = "../PII/Pictures/P" + id + edit("99",child);
      do n = 1 while FileExist( textstr2 + "_" + edit("99",n) + ".jpg" ) enddo;
      textstr2 = textstr2 + "_" + edit("99",n) + ".jpg";
      VCardPic.save( textstr2 );	  
      view( textstr2 );
      if accept( tr("Do you want to take another picture of the document"), tr("Yes"), tr("No") ) <> 1 then
        break
      endif;
    enddo;
  end;




  {  { function to close the interview }
  function endmess();
    { Returns true if response is REVIEW }
    endmess = ( demode() = add &
               accept("Конец вопросника",
                      "Вернуться",
                      "Закончить ввод данных") <> 2);
  end;
  }

  function onStop()
     if demode() = add then
       if (errmsg(0014) select("Да", continue, "Нет", continue)) <> 1 then //!T
         reenter getsymbol();
       endif;
     else
       if (errmsg(0015) select("Да", continue, "Нет", continue)) = 1 then //!T
         advance;
       endif;
     endif;
  end;

PROC FACVACC_FF
preproc
  numeric ss = sysdate("yyyymmdd");
  dd = ss - 100*(int(ss/100));
  ss = int((ss-dd)/100);
  mm = ss - 100*(int(ss/100));
  yy = int((ss-mm)/100);

  { set language at start of the program, defaulting to language passed by menu }
  setlanguage(loadsetting("Language", getlanguage()));

  setfont( ALL, "Arial", 16, bold );


  set behavior() exit on;

  { set up minimal user bar }
  userbase();
  userbar( show );
  
 
  { converts parameter to numbers  }
  xcluster = tonumber( sysparm()[1:4] );     { cluster number } 
  xsuperv  = tonumber( sysparm()[5:4] );     { supervisor number }
   xinterv  = tonumber( sysparm()[1:4] );    { interviewer number }
  xhhnum   = tonumber( sysparm()[14:4] );    { household number }
  xmline   = tonumber( sysparm()[18:2] );    { individual line number }
  
  //loading household
  QHCLUST = xcluster;
  QHNUMBER = xhhnum;
  QHSUPERV = xsuperv;
  QHINTNUM = xinterv;
  
  if !loadcase(TJHH80, QHCLUST, QHNUMBER) then
    errmsg("Error loading household data");
    stop(-1);
  endif;

  //loading mother
  QCLUSTER = xcluster;
  QNUMBER = xhhnum;
  QLINE = xmline;
 
  if !loadcase(TJIN80, QCLUSTER, QNUMBER, QLINE) then
    errmsg("Error loading mother's data");
    stop(-1);
  endif;
  
  //getting child's column index in the vacctionation section
  xccol = 0;
  do i = 1 while i <= noccurs(TJIN80.QWSEC05) by 1
    if QCOL51(i) = xcline then
      xccol = i;
      break;
    endif;
  enddo;
  
  if !xccol then
    errmsg("Child not found in section 14 of mother's questionnaire");
    stop(-1);
  endif;

postproc
  stop(-1);
  
PROC FVCHILD
preproc
 // hideWaitScreen();
  
postproc
  endlevel;
  
PROC FRCLUST
preproc
 $ = QHCLUST;
 
PROC FRHNUMBER
preproc
  $ = QHNUMBER;
  
PROC FRSUPERV
preproc
  $ = xsuperv
  
PROC FRHHNAME
preproc
  $ = strip(QH02(1));
  
PROC FRINTERV
preproc
  $ = xinterv
PROC FRMNAME
preproc
  $ = strip(FA04A(xccol)) + " " + strip(FA04B(xccol));
  
PROC FRMLINE
preproc
  $ = xmline;
  
PROC FRCNAME
preproc
  $ = strip(FA03A(xccol)) + " " + strip(FA03B(xccol));
  
PROC FRCLINE
preproc
  $ = xcline;
  
PROC FRCDOBD
preproc
 $ = Q508D(xccol);
PROC FRCDOBM
preproc
 $ = Q508M(xccol);
PROC FRCDOBY
preproc
 $ = Q508Y(xccol);
PROC FRDNAME
preproc
  $ = strip(FA07A(xccol)) + " " + strip(FA07B(xccol));
PROC FRFNAME
preproc
  $ = strip(FA06A(xccol)) 
PROC FRFADDR
preproc
  $ = strip(FA06B(xccol)) 
PROC FRFPHONE
preproc
  $ = strip(FA06C(xccol)) 
PROC FRUCHN
preproc
 $ = FA08(xccol);
PROC FR02
  if $ <> 1 then
    skip to FRFINAL
  endif
PROC FR03N
preproc
  $ = curocc();
  
PROC FR03D
onfocus
  vaccname = getlabel(FR03N, FR03N(curocc()));

  
PROC FR03M
preproc 
  if FR03D in 0, 44 then
    $ = FR03D;
    noinput;
  endif;

onfocus
  vaccname = getlabel(FR03N, FR03N(curocc()));
  
PROC FR03Y
preproc 
  if FR03D in 0, 44 then
    $ = FR03D * 100 + FR03D;
    noinput;
  endif;

onfocus
  vaccname = getlabel(FR03N, FR03N(curocc()));

postproc
vcheck( FR03D, FR03M, $ );
vckbirth( FR03D, FR03M, $, FRCLINE);
PROC FR04
  if demode() = add & $ = 1 then
    TakePictureW( FRCLINE )
    elseif $ <> 1 then
      skip to FRFINAL
  endif;	

PROC FR05
preproc
  if FR04 = 1 then
    $ = 1
  elseif FR04 <>1 then
	$ = 2
  endif;  
PROC FRFINAL
onfocus
  $ = " ";

postproc
  if demode() = add then
    editnote();
    if endmess() then
      reenter
    else
      endlevel;
    endif;
  else
    endlevel;
  endif;
